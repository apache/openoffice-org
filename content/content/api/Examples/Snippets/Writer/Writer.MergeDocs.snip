<!-- ========================================================================
$RCSfile: Writer.MergeDocs.snip,v $

last change: $Revision: 1.4 $ $Author: tomsontom $ $Date: 2004/08/04 08:21:47 $

(c)2003 by the copyright holders listed with the author-tags.
If no explicit copyright holder is mentioned with a certain author,
the author him-/herself is the copyright holder. All rights reserved.

Public Documentation License Notice:

The contents of this Documentation are subject to the 
Public Documentation License Version 1.0 (the "License"); 
you may only use this Documentation if you comply with 
the terms of this License. A copy of the License is 
available at http://www.openoffice.org/licenses/PDL.html

The Original Documentation can be found in the CVS archives 
of openoffice.org at the place specified by RCSfile: in this header.

The Initial Writer(s) of the Original Documentation are listed 
with the author-tags below.

The Contributor(s) are listed with the author-tags below 
without the marker for being an initial author.

All Rights Reserved.
======================================================================== -->

<snippet language="OOBasic" application="Writer">
<keywords>
    <keyword>LoadComponentFromUrl</keyword>
    <keyword>InsertDocumentFromUrl </keyword>
    <keyword>StoreAsURL</keyword>
    <keyword>Merge</keyword>
    <keyword>Insert</keyword>
</keywords>

<authors>
	<author id="lau" initial="true" email="listes.godard@laposte.net">Laurent Godard</author>
	<author id="and" email="andrew@pitonyak.org">Andrew Pitonyak</author>
	<author id="mi" email="Michael.Hoennig@Sun.Com">Michael H&#246;nnig</author>
	<author id="tomsontom" initial="false" email="tom.schindl@bestsolution.at">Tom Schindl</author>
</authors>

<question heading="insert document into another">
<p>How can I insert one document into another?</p>

<p>I run a mail merge, which creates one file for each address. I want to merge all of the documents into one document. </p>
</question>

<answer>
<p>
This utility retrieves all write documents in a directory and creates a single output file that contains all of the documents combined into one. A page break is inserted before each new document.
</p>

<listing>
Sub MergeDocumentsInDirectory()
  
  Dim DestDirectory As String
  Dim FileName As String
  Dim SrcFile As String, DstFile As String 
  Dim oDesktop, oDoc, oCursor, oText
  Dim argsInsert()
  Dim args()
  ' Remove the following comments to do things hidden
  ' dim args(0) as new com.sun.star.beans.PropertyValue
  ' args(0).name = "Hidden"
  ' args(0).value = true
 
  ' Which desitnation directory?
  DestDirectory = Trim( GetFolderName() )

  If DestDirectory = "" Then
    MsgBox "No directory selected, exiting",16,"Merging Documents"
    Exit Sub
  End If

  ' force a trailing backslash. This is okay because using URL notation
  If Right(DestDirectory,1) &lt;&gt; "/" Then
    DestDirectory = DestDirectory &amp; "/"
  End If

  oDeskTop = CreateUnoService("com.sun.star.frame.Desktop")
 
  ' Read the first file!
  FileName = Dir(DestDirectory)
  DstFile = ConvertToURL(DestDirectory &amp; "ResultatFusion.sxw")
  Do While FileName &lt;&gt; ""
    If lcase( right(FileName,3)) = "sxw" Then
      SrcFile = ConvertToURL(DestDirectory &amp; FileName)
      If IsNull(oDoc) OR IsEmpty(oDoc) Then
        FileCopy( SrcFile, DstFile )
        oDoc = oDeskTop.Loadcomponentfromurl(DstFile, "_blank", 0, Args())
        oText = oDoc.getText
        oCursor = oText.createTextCursor()
      Else
        oCursor.gotoEnd(false)
        oCursor.BreakType = com.sun.star.style.BreakType.PAGE_BEFORE
        oCursor.insertDocumentFromUrl(SrcFile, argsInsert())
      End If
    End If
    FileName = dir()
  Loop
 
  If IsNull(oDoc) OR IsEmpty(oDoc) Then
    MsgBox "No documents merged!",16,"Merging Documents" 
    Exit Sub
  End If

  ' Save the document
  Dim args2()
  oDoc.StoreAsURL(DestDirectory &amp; "ResultatFusion.sxw",args2())
  If HasUnoInterfaces(oDoc, "com.sun.star.util.XCloseable") Then
    oDoc.close(true)
  Else
    oDoc.dispose
  End If
 
  ' Reload the document!
  oDoc=oDeskTop.Loadcomponentfromurl(DstFile,"_blank",0,Args2())
End Sub
</listing>

<p>This macro demonstrates many features in OOo. 
For example, the correct mechanism is used to close an existing document. 
The close method is used if it is supported and dispose is used if it is not supported. 
As of OOo 1.1, close should be supported.  </p>

<p>Although the user is asked which directory should contain the combined file, the name of the combined file is hard coded to be "ResultatFusion.sxw". 
If this file already exists, it will probably be overwritten. 
You have been warned! 
The first Write file found is copied to the destination file, overwriting any existing file. 
The file is then opened. Each additional file is appended to the end of this file. 
When the process is completed, the file is saved to disk and then reopened.</p>

</answer>

<changelog>
	<change author-id="tomsontom" date="2004-06-22">Modified to match codesnippet.dtd v2.0, added syntax highlighting</change>
	<change author-id="lau" date="0000-00-00">initial version</change>
</changelog>

</snippet> 
