<!--
$RCSfile: Office.ContexMenuInterceptor.snip,v $

last change: $Revision: 1.1 $ $Author: tomsontom $ $Date: 2004/11/18 07:22:25 $

(c)2003 by the copyright holders listed with the author-tags.
If no explicit copyright holder is mentioned with a certain author,
the author him-/herself is the copyright holder. All rights reserved.

Public Documentation License Notice:

The contents of this Documentation are subject to the
Public Documentation License Version 1.0 (the "License");
you may only use this Documentation if you comply with
the terms of this License. A copy of the License is
available at http://www.openoffice.org/licenses/PDL.html

The Original Documentation can be found in the CVS archives
of openoffice.org at the place specified by RCSfile: in this header.

The Initial Writer(s) of the Original Documentation are listed
with the author-tags below.

The Contributor(s) are listed with the author-tags below
without the marker for being an initial author.

All Rights Reserved.
-->

<snippet language="OOBasic" application="Office">

<keywords>
	<keyword>registerContextMenuInterceptor</keyword>
	<keyword>releaseContextMenuInterceptor</keyword>
	<keyword>ContextMenuInterceptor</keyword>
	<keyword>ActionTrigger</keyword>
	<keyword>PropertySet</keyword>
	<keyword>PersistentPropertySet</keyword>
	<keyword>PropertySetRegistry</keyword>
	<keyword>CreateUnoListener</keyword>
</keywords>

<authors>
	<author id="paolomantovani" initial="true" email="paolomantovani@openoffice.org">Paolo Mantovani</author>
</authors>

<question heading="Managing context menues in OOBasic">
Is possible to manage context menues in OOBasic ?
</question>

<answer>
<p>I&apos;ve just implemented a contextMenuInterceptor in OOBasic.</p>
<p>It works quite well and it&apos;s able to add and remove arbitrary menu items.</p>
<p>The interceptor is made with an &quot;illegal&quot; call to CreateUnoListener</p>
<p>The following step was to add the ability to add menu items.</p>
<p>As you know, a context menu item should be a service that supports the com.sun.star.beans.XPropertySet</p>
<p>The property set should contain attributes like CommandURL, HelpURL as defined in the service com.sun.star.ui.ActionTrigger</p>
<p>The main problem is that OOBasic does not support the implementation of customized Uno services.</p>
<p>To workaround, I used a PersistentPropertySet instead of a customized css.beans.XPropertySet.</p>
<p>See the example code:</p>
<listing>
REM  *****  BASIC  *****

Option Explicit

Global oDocView As Object
Global oContextMenuInterceptor As Object

Global oStore As Object
Global oPropSetRegistry As Object

Const MNU_PREFIX = &quot;pmxMenu_&quot;


&apos;_______________________________________________________________________________
Sub registerContextMenuInterceptor
	
	InitMenuFactory

	oDocView = ThisComponent.CurrentController
	oContextMenuInterceptor = _
	CreateUnoListener(&quot;ThisDocument_&quot;, &quot;com.sun.star.ui.XContextMenuInterceptor&quot;)
	
	oDocView.registerContextMenuInterceptor(oContextMenuInterceptor)
	
End Sub


&apos;_______________________________________________________________________________
Sub releaseContextMenuInterceptor

	On Error Resume Next
	oDocView.releaseContextMenuInterceptor(oContextMenuInterceptor)
	
	TerminateMenuFactory
	
End Sub


&apos;_______________________________________________________________________________
Function ThisDocument_notifyContextMenuExecute(ContextMenuExecuteEvent As Object) As Variant
Dim oSrcWin As Object
Dim oExePoint As Object
Dim oATContainer As Object
Dim oSelection As Object

Dim oMenuItem As Object
Dim I As Integer

	With ContextMenuExecuteEvent
	
		&apos;contains the window where the context
		&apos;menu has been requested 
		oSrcWin = .SourceWindow
		
		&apos;contains the position the context menu
		&apos;will be executed at (css.awt.Point)
		oExePoint = .ExecutePosition
		
		&apos;enables the access to the menu content. 
		&apos;The implementing object has to support the
		&apos;service ActionTriggerContainer
		oATContainer = .ActionTriggerContainer 
		
		&apos;provides the current selection
		&apos;inside the source window
		oSelection = .Selection 				
	End With


	&apos;remove all menu entries:
	For I = oATContainer.Count - 1 To 0 Step -1
		oATContainer.removeByIndex(I)
	Next I
	
	&apos;add some context menu entry
	oMenuItem = GetSimpleMenuItem(&quot;Entry1&quot;, &quot;Paolo Mantovani Was Here&quot;, &quot;&quot;)
	oATContainer.insertByIndex(0, oMenuItem)
	
	oMenuItem = GetMenuSeparator(&quot;Entry2&quot;)
	oATContainer.insertByIndex(1, oMenuItem)
	
	oMenuItem = GetSimpleMenuItem(&quot;Entry3&quot;, &quot;Run the Snippet Creator&quot;, &quot;macro:///SnippetCreator.Main.Main&quot;)
	oATContainer.insertByIndex(2, oMenuItem)


&apos; POSSIBLE RESULTS FOR THIS FUNCTION
&apos; This function must result one of the following values:

&apos; com.sun.star.ui.ContextMenuInterceptorAction.IGNORED
&apos;	the XContextMenuInterceptor has ignored the call. 
&apos;	The next registered XContextMenuInterceptor should be notified.

&apos; com.sun.star.ui.ContextMenuInterceptorAction.CANCELLED
&apos;	the context menu must not be executed. 
&apos;	The next registered XContextMenuInterceptor should not be notified.

&apos; com.sun.star.ui.ContextMenuInterceptorAction.EXECUTE_MODIFIED
&apos;	the menu has been modified and should be executed
&apos;	without notifying the next registered XContextMenuInterceptor.

&apos; com.sun.star.ui.ContextMenuInterceptorAction.CONTINUE_MODIFIED
&apos;	the menu has been modified and the next registered
&apos;	XContextMenuInterceptor should be notified.

	ThisDocument_notifyContextMenuExecute = _
		com.sun.star.ui.ContextMenuInterceptorAction.EXECUTE_MODIFIED

End Function


&apos;_______________________________________________________________________________
&apos;	MENU FACTORY ROUTINES
&apos;_______________________________________________________________________________
Sub InitMenuFactory()

	oStore = CreateUnoService(&quot;com.sun.star.ucb.Store&quot;)
	oPropSetRegistry = oStore.createPropertySetRegistry(&quot;&quot;)

End Sub


&apos;_______________________________________________________________________________
Sub TerminateMenuFactory()
Dim mNames()
Dim sName As String
Dim I As Integer

	mNames() = oPropSetRegistry.getElementNames
	
	For I = LBound(mNames()) To UBound(mNames())
		sName = mNames(I)
		
		If Left(sName, Len(MNU_PREFIX)) = MNU_PREFIX Then
			oPropSetRegistry.removePropertySet ( sName ) 
		End If
	Next I
	
	oPropSetRegistry.dispose
	oStore.dispose
	
End Sub

&apos;_______________________________________________________________________________
&apos; Sorry: menu icon and sub-menues not supported
Function GetSimpleMenuItem( sName As String, sText As String, _
sCommandUrl As String, Optional sHelpUrl As String ) As Object

Dim oPropSet As Object
Dim sInternalName As String

	sInternalName = MNU_PREFIX &amp; sName
	If oPropSetRegistry.hasByName(sInternalName) Then
		oPropSetRegistry.removePropertySet(sInternalName)
	End If
		
	oPropSet = oPropSetRegistry.openPropertySet(sInternalName, True)

	oPropSet.addProperty(&quot;Text&quot;, 0, sText)
	oPropSet.addProperty(&quot;CommandURL&quot;, 0, sCommandUrl)

	If Not IsMissing(sHelpUrl) Then
		oPropSet.addProperty(&quot;HelpURL&quot;, 0, sHelpUrl)
	End If
		
	GetSimpleMenuItem = oPropSet
	
End Function


&apos;_______________________________________________________________________________
Function GetMenuSeparator( sName As String ) As Object

Dim oPropSet As Object
Dim sInternalName As String
Dim iSeparatorType As Integer

	sInternalName = MNU_PREFIX &amp; sName
	If oPropSetRegistry.hasByName(sInternalName) Then
		oPropSetRegistry.removePropertySet(sInternalName)
	End If
	
	oPropSet = oPropSetRegistry.openPropertySet(sInternalName, True)
	
	&apos;constant group com.sun.star.ui.ActionTriggerSeparatorType not supported?
	&apos;unfortunately, the only separator-type working is the &quot;SPACE&quot;
	&apos;regardless for the iSeparatorType passed...
	iSeparatorType = 1
	oPropSet.addProperty(&quot;SeparatorType&quot;, 0, iSeparatorType)

	GetMenuSeparator = oPropSet
	
End Function

</listing>
</answer>

<changelog>
	<change author-id="paolomantovani" date="2004-11-02">Initial version</change>
</changelog>

</snippet>

